@ECHO OFF
GOTO START

:START

	REM LOAD VARS FROM CONFIG FILE.
	CALL GAME-DATA-CONFIG.BAT
	
	REM SET OTHER VARIABLES.
	SET SAINT_COINACH_DIR=%GAME_DATA_DIR%\SaintCoinach
	SET TOOLS_DIR=%GAME_DATA_DIR%\tools
	SET EXTRACTS_DIR=%GAME_DATA_DIR%\SaintCoinachExtracts
	SET CONFIGURATION=RELEASE

	REM CHECK IF SAINT COINACH DIR EXISTS.
	IF EXIST %SAINT_COINACH_DIR% (
		GOTO UPDATE
	) ELSE (
		GOTO INSTALL
	)

REM UPDATE SAINT COINACH.
:UPDATE
	cd %SAINT_COINACH_DIR%
	git checkout .
	git pull origin
	GOTO BUILD
		
REM INSTALL SAINT COINACH.
:INSTALL
	git clone https://github.com/ufx/SaintCoinach.git
	cd %SAINT_COINACH_DIR%
	GOTO BUILD

REM BUILD SAINT COINACH.
:BUILD
	RMDIR /s /q %SAINT_COINACH_DIR%\SaintCoinach.Cmd\bin\Release
	RMDIR /s /q %EXTRACTS_DIR%\
	CD %TOOLS_DIR%
	NUGET.EXE RESTORE ../SAINTCOINACH/SAINTCOINACH.SLN
	cd %SAINT_COINACH_DIR%
	%DEVENV% .\SAINTCOINACH.SLN /Rebuild %CONFIGURATION%
	GOTO EXTRACT
	
REM GENERATE RAW DATA FILES.
:EXTRACT
	CD /D %SAINT_COINACH_DIR%\SaintCoinach.Cmd\bin\Release
	SaintCoinach.Cmd %FFXIV_DIR% rawexd
	MOVE ./Definitions ../Definitions
	for /F "tokens=*" %%x in ('dir /ad /b') do (set OUTPUT_DIR=%%x)
	MOVE ../Definitions ./Definitions
	xcopy %OUTPUT_DIR%\rawexd %EXTRACTS_DIR%\ /E
	GOTO PARSE

REM CREATE PROPERTY FILES FROM EXTRACTS.
:PARSE
	CD /D %GAME_DATA_DIR%
	python transform-saint-coinach.py

pause

